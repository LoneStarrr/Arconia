buildscript {
    repositories {
        mavenCentral()
        maven {
            name "Forge"
            url "https://files.minecraftforge.net/maven"
        }
        // Sponge - required for mixins
        maven {
            name "Sponge"
            url "https://repo.spongepowered.org/repository/maven-public"
        }

        // Tests
        jcenter()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: "${mixingradle_version}"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'


mixin {
    add sourceSets.main, "arconia.refmap.json"
}


version = "${mc_version}-${mod_version}"
group = "lonestarrr.arconia" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'arconia'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    // Get your mappings here: http://export.mcpbot.bspk.rs/
    // Nope. Now it's: !mcpv in a DM to forge-bot in discord
    mappings channel: "${mappings_channel}", version: "${mappings_version}"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')
            arg "-mixin.config=arconia.mixins.json"


            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                arconia {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            arg "-mixin.config=arconia.mixins.json"

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                arconia {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'arconia', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                arconia {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    // JEI repo, from https://github.com/mezz/JustEnoughItems/wiki/Getting-Started-%5B1.13-and-Up%5D
    maven {
        url = 'https://dvs1.progwml6.com/files/maven/'
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }

    // Patchouli - for the guide book
    maven {
        name = "Patchouli"
        url = "https://maven.blamejared.com"
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
    // The One Probe
    maven {
        url = "https://maven.k-4u.nl"
        metadataSources {
            mavenPom()
            artifact()
            ignoreGradleMetadataRedirection()
        }
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    // Required for MixinGradle - note that this is the Spongepowered Mixin version, not the version of MixinGradle
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    // You may put jars on which you depend on in ./libs or you may define them like so..
    // compile "some.group:artifact:version:classifier"
    // compile "some.group:artifact:version"

    // Real examples
    // compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    // compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // The 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    // provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // These dependencies get remapped to your current MCP mappings
    // deobf 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // For more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    // TODO everyone and their dog uses junit-jupiter-api instead, why?
    // Looks like I followed: https://junit.org/junit5/docs/current/user-guide/#running-tests-build-gradle

    // 2022-02-07:
    //  All the 'transitive = false' below is there because these dependencies pull in their own dependencies, one of
    //  which is a deobfuscated 'fake' dependency that will never work, this one:
    //  - Dependencies that have names ending in _mapped_[stable/snapshot/official]_[Mapping Version] are NOT hosted anywhere, they are generated by ForgeGradle on the fly and inserted into Gradle through a fake repository, when one of these dependencies is reported missing it is a sign that errors occurred generating them.

    // JEI
    // compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}:api") { transitive = false }
    // at runtime, use the full JEI jar
    runtimeOnly fg.deobf("mezz.jei:jei-${mc_version}:${jei_version}") { transitive = false }

    // Patchouli
    compileOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}:api") { transitive = false }
    runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}") { transitive = false }

    // The One Probe
    compileOnly fg.deobf("mcjty.theoneprobe:theoneprobe:${top_version}") { transitive = false }
    runtimeOnly fg.deobf("mcjty.theoneprobe:theoneprobe:${top_version}") { transitive = false }

    // Test framework
    testImplementation "org.junit.jupiter:junit-jupiter:${junit_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"

}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": "arconia",
            "Specification-Vendor": "LoneStarrr",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"LoneStarrr",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "arconia.mixins.json"
        ])
    }
}

// 2022-02-06 from forge discord - this should exclude forge from transitive dependencies from other mods
// tasks.withType(GenerateModuleMetadata) {
//     enabled = false
// }

// Example configuration to allow publishing using the maven-publish task
// we define a custom artifact that is sourced from the reobfJar output task
// and then declare that to be published
// Note you'll need to add a repository here
def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact

            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if (dep.groupId.last().value().last() in ["net.minecraftforge", "mezz.jei"]) {
                        assert dep.parent().remove(dep)
                    }
                }
            }

        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
